// ------------------
// definition entries
// ------------------
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return '-' + stdout.toString().trim()
}

def getSprintRound = { ->
    if (project.hasProperty("SPRINT_ROUND")) {
        return project.property("SPRINT_ROUND")
    } else {
        return ''
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Build config types set by the variants
final String CONFIG_TYPE_BOOLEAN = 'boolean'

// Build config fields set by the variants
final String CONFIG_IS_NONPROD_BUILD = 'IS_NONPROD_BUILD'

// check that the SEPTA.signing gradle property exists and that its value is the filename to a file
//  that has the extension of gradle
if (project.hasProperty("SEPTA.signing") && file(project.property("SEPTA.signing") + ".gradle").exists()) {
    apply from: project.property("SEPTA.signing") + ".gradle"
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "org.septa.android.app"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 297
        versionName "2.0.297"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        prerelease {
            storeFile file("${SEPTA_DEVELOPMENT_KEYSTORE_FILE}")
            storePassword SEPTA_DEVELOPMENT_KEYSTORE_PASSWORD
            keyAlias 'prerelease'
            keyPassword PRERELEASE_KEYALIAS_PASSWORD
        }
        alpha {
            storeFile file("${SEPTA_DEVELOPMENT_KEYSTORE_FILE}")
            storePassword SEPTA_DEVELOPMENT_KEYSTORE_PASSWORD
            keyAlias 'alpha'
            keyPassword ALPHA_KEYALIAS_PASSWORD
        }
        beta {
            storeFile file("${SEPTA_DEVELOPMENT_KEYSTORE_FILE}")
            storePassword SEPTA_DEVELOPMENT_KEYSTORE_PASSWORD
            keyAlias 'beta'
            keyPassword BETA_KEYALIAS_PASSWORD
        }
        release {
            storeFile file("${SEPTA_DEVELOPMENT_KEYSTORE_FILE}")
            storePassword SEPTA_DEVELOPMENT_KEYSTORE_PASSWORD
            keyAlias 'prerelease'
            keyPassword PRERELEASE_KEYALIAS_PASSWORD
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            zipAlignEnabled false
            signingConfig signingConfigs.prerelease
            versionNameSuffix "d" + "${getSprintRound}" + "${getGitHash}"
            buildConfigField "String", "VERSIONNAME", "\"${defaultConfig.versionName}-b${defaultConfig.versionCode}\""
            buildConfigField CONFIG_TYPE_BOOLEAN, CONFIG_IS_NONPROD_BUILD, true.toString()
            manifestPlaceholders = [amazonawsApiKey: "${AMAZONAWS_API_KEY_DEV}", googleGeoApiKey: "${GOOGLE_GEO_API_KEY}", crashlyticsApiKey: "${CRASHLYTICS_API_KEY}"]
        }
        alpha {
            minifyEnabled false
            debuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.prerelease
            versionNameSuffix "a" + "${getSprintRound}" + "${getGitHash}"
            buildConfigField "String", "VERSIONNAME", "\"${defaultConfig.versionName}-b${defaultConfig.versionCode}\""
            buildConfigField CONFIG_TYPE_BOOLEAN, CONFIG_IS_NONPROD_BUILD, true.toString()
            manifestPlaceholders = [amazonawsApiKey: "${AMAZONAWS_API_KEY_QA}", googleGeoApiKey: "${GOOGLE_GEO_API_KEY}", crashlyticsApiKey: "${CRASHLYTICS_API_KEY}"]
        }
        beta {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            debuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.prerelease
            versionNameSuffix "b" + "${getSprintRound}" + "${getGitHash}"
            buildConfigField "String", "VERSIONNAME", "\"${defaultConfig.versionName}-b${defaultConfig.versionCode}\""
            buildConfigField CONFIG_TYPE_BOOLEAN, CONFIG_IS_NONPROD_BUILD, true.toString()
            manifestPlaceholders = [amazonawsApiKey: "${AMAZONAWS_API_KEY_QA}", googleGeoApiKey: "${GOOGLE_GEO_API_KEY}", crashlyticsApiKey: "${CRASHLYTICS_API_KEY}"]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            debuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.prerelease
            buildConfigField "String", "VERSIONNAME", "\"${defaultConfig.versionName}\""
            buildConfigField CONFIG_TYPE_BOOLEAN, CONFIG_IS_NONPROD_BUILD, false.toString()
            manifestPlaceholders = [amazonawsApiKey: "${AMAZONAWS_API_KEY_PROD}", googleGeoApiKey: "${GOOGLE_GEO_API_KEY}", crashlyticsApiKey: "${CRASHLYTICS_API_KEY}"]
        }
    }
    productFlavors {
    }

}

repositories {
    mavenCentral()
    maven { url 'http://mvnrepository.com/artifact/' }
    maven { url 'https://maven.google.com' }
    maven { url 'https://maven.fabric.io/public' }
}

// buildscript
buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://maven.google.com' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'io.fabric.tools:gradle:1.25.4'

    }
}


apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.google.android.gms:play-services-places:15.0.1'
    implementation 'com.android.support:support-core-utils:27.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-messaging:17.1.0'
    testImplementation 'junit:junit:4.12'
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0') {
        transitive = true
    }
}